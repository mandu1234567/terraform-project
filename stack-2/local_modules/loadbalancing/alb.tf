resource "aws_lb_target_group" "alb_target_group" {
  name     = "${var.env_name}-frontend-tg"
  port     = 8501
  protocol = "HTTP"
  vpc_id   = var.vpc_id

  health_check {
    protocol = "HTTP"
    port     = 8501
    path     = "/"
  }

  tags = {
    Name = "${var.env_name}-frontend-tg"
  }
}

resource "aws_lb" "alb" {
  name               = "${var.env_name}-frontend-lb"
  internal           = false
  load_balancer_type = "application"
  subnets            = var.public_subnet_ids
  enable_deletion_protection = false
  security_groups    = [aws_security_group.alb_sg.id]

  tags = {
    Name = "${var.env_name}-frontend-alb"
  }
}

resource "aws_lb_listener" "alb_listener" {
  load_balancer_arn = aws_lb.alb.arn
  port              = 80
  protocol          = "HTTP"

  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.alb_target_group.arn
  }
}

resource "aws_security_group" "alb_sg" {
  name   = "${var.env_name}-frontend-alb-sg"
  vpc_id = var.vpc_id

  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}